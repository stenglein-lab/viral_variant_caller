#!/usr/bin/perl

use strict;
use Data::Dumper;
use Getopt::Long;


my $usage = <<USAGE;

 This script outputs a table containing information about a series of variants relative to some reference sequence

 this script takes as input:

  a GFF file -> containing annotation for a reference sequence
  a VCF file -> containing variants relative to the reference sequence

 It then outputs a table, with a row for each variant described in the VCF file and 
 the following tab-delimited fields

 CDS -> name of CDS region (if variant is in a coding sequence)
 the reference AA
 the codon number (in the CDS)
 the variant AA
 whether it's a synonymous or non-syn mutation
 the variant's position in the reference sequence
 the variant's position in the CDS
 the reference sequence codon
 the variant codon
 the reference base
 the variant base
 the variant's fraction, as defined by the DP4 field in the VCF file 


 Mark Stenglein, 3/27/2017


  usage: $0 [-h] [-s] gff_file vcf_files

    - h    print this help message

    - s    same_codon: only output variants for codons for which more than one variant was detected


USAGE

if ((scalar @ARGV == 0) and -t STDIN) { print $usage and exit; }

my $print_usage = 0;
my $same_codon_only = 0;

GetOptions ("h" => \$print_usage, "s" => \$same_codon_only);


my $gff_file = shift or die "$usage\n";
my $vcf_file = shift or die "$usage\n";

open (my $gff_fh, "<", $gff_file) or die ($!);
open (my $vcf_fh, "<", $vcf_file) or die ($!);

my %cds = ();
# my %cd_to_genome_map = ();
my %genome_to_cd_map = ();
my %cds_seqs = ();
my %basemap = ();
# my %seqs = ();

# parse gff file -> store annotations
parse_gff($gff_fh);

# assume std genetic code (table below)
my %genetic_code;
initialize_genetic_code();

my %histo = ();
my %lengths = ();
my %variants = ();

my %all_variants = ();
my %codons_hit = ();
my $variant_number = 1;


# read in VCF file and store variants
while (<$vcf_fh>)
{
   chomp;

   if (/^#/) 
   { 
      if (/##contig=<ID=(\S+),length=(\d+)>/)
      {
         $lengths{$1} = $2;
      }
      next;  # ignore most header lines
   } 

   my @data = split;

   my $contig = $data[0];

   # CHROM   POS   ID REF   ALT 
   my $position = $data[1];
   my $ref = $data[3];
   my @all_alt = split ",", $data[4];
   my $alt = shift @all_alt;

   if ($alt eq '<X>')
   {
      next;
   }

   my $ref_depth = undef;
   my $variant_depth = undef;
   my $overall_depth = undef;

   my @matched_depths = ();
   if (@matched_depths = /DP=(\d+).*DP4=(\d+),(\d+),(\d+),(\d+)/)
   {
      if (scalar @matched_depths != 5)
      {
         warn "error: unexpected format in VCF for line $_\n"; exit;
      }

      my ($depth, $d1, $d2, $d3, $d4) = @matched_depths;
      $overall_depth = $depth;

      $ref_depth = $d1 + $d2;
      $variant_depth = $d3 + $d4;
   }
   else
   {
      warn "ignoring line with unexpected format in VCF: $_\n";
   }

   process_variant ($contig, $position, $ref, $alt, $ref_depth, $variant_depth, $overall_depth);

}
close $vcf_fh;

output_variants();

sub output_variants
{
   # print header line
   # print "CDS\tref_aa\tcodon_number\tvariant_aa\tSynonymous or non\tposition in genome\tposition in CDS\tref_codon\tvariant_codon\tref base\tvariant base\tvariant_fraction\n";
	foreach my $variant_number (sort {$a<=>$b} keys %all_variants)
	{
      my $cds_hit = $all_variants{$variant_number}{cds_hit};
      my $ref_aa = $all_variants{$variant_number}{ref_aa};
      my $codon_number = $all_variants{$variant_number}{codon_number};
      my $alt_aa = $all_variants{$variant_number}{alt_aa};
      my $N_or_S = $all_variants{$variant_number}{N_or_S};
      my $position = $all_variants{$variant_number}{position};
      my $position_in_cds = $all_variants{$variant_number}{position_in_cds};
      my $ref_codon = $all_variants{$variant_number}{ref_codon};
      my $alt_codon = $all_variants{$variant_number}{alt_codon};
      my $ref = $all_variants{$variant_number}{ref};
      my $alt = $all_variants{$variant_number}{alt};
      my $variant_fraction = $all_variants{$variant_number}{variant_fraction};

		# check to see if only going to output hits to the same codon...
		if ($same_codon_only)
		{
		   if ($codons_hit{$cds_hit}{$codon_number} <= 1)
			{
			   next;
			}
		}

		print "$cds_hit\t$ref_aa\t$codon_number\t$alt_aa\t$N_or_S\t$position\t$position_in_cds\t$ref_codon\t$alt_codon\t$ref\t$alt\t$variant_fraction\n";

   }
}

sub process_variant
{
   my ($seg_id, $position, $ref, $alt, $ref_depth, $variant_depth, $depth) = @_;

   my $variant_fraction = sprintf ("%0.2f", ($variant_depth / $depth) );

   # Default values in case not in an annotated position
	my $cds_hit = "NA";
	my $position_in_cds = "NA";
   my $codon_number = "NA";
   my $ref_codon = "NA";
   my $alt_codon = "NA";

   my $ref_aa = "NA";
   my $alt_aa = "NA";
   my $N_or_S = "NA";

	# in an annotated position
   if (defined $basemap{$position})
   {
      # this is in an annotated feature
      my @cds_hit = @{$basemap{$position}};
      my $cds_hit_string = join "+", @cds_hit;
      # warn "$position\t$cds_hit_string\t$depth\t$ref_depth\t$variant_depth\n";
      if (scalar @cds_hit > 1)
      {
         warn "** more than one CDS hit! **\n";
      }
      foreach my $this_cds_hit (@cds_hit)
      {
			$cds_hit = $this_cds_hit;
         $position_in_cds = $genome_to_cd_map{$cds_hit}{$position};
         my $cds_seq = $cds_seqs{$cds_hit};
         my $ref_base = substr($cds_seq, ($position_in_cds-1), 1);
         if ($ref_base ne $ref)
         {
            warn "error in reference base.  got: $ref_base.  expecting: $ref   CDS: $cds_hit    pos: $position:    pos_in_cds: $position_in_cds\n";
            die "cds_seq: $cds_seq\n";
         }
         my $alt_cds = $cds_seq;
         # change coding sequence with variant base
         substr($alt_cds, ($position_in_cds - 1), 1) = $alt;
         $codon_number = int(($position_in_cds-1)/3) + 1;
         $ref_codon = substr ($cds_seq, (($codon_number - 1) * 3), 3);
         $alt_codon = substr ($alt_cds, (($codon_number - 1) * 3), 3);

         $ref_aa = translate($ref_codon);
         $alt_aa = translate($alt_codon);
         $N_or_S = "S";
         if ($ref_aa ne $alt_aa)
         {
            $N_or_S = "N";
         }

			# keep track of how many times each codon was hit
			$codons_hit{$cds_hit}{$codon_number} += 1;

			# store info on variant
         # print "$cds_hit\t$ref_aa\t$codon_number\t$alt_aa\t$N_or_S\t$position\t$position_in_cds\t$ref_codon\t$alt_codon\t$ref\t$alt\t$variant_fraction\n";
         $all_variants{$variant_number}{cds_hit} = $cds_hit;
         $all_variants{$variant_number}{ref_aa} = $ref_aa;
         $all_variants{$variant_number}{codon_number} = $codon_number;
         $all_variants{$variant_number}{alt_aa} = $alt_aa;
         $all_variants{$variant_number}{N_or_S} = $N_or_S;
         $all_variants{$variant_number}{position} = $position;
         $all_variants{$variant_number}{position_in_cds} = $position_in_cds;
         $all_variants{$variant_number}{ref_codon} = $ref_codon;
         $all_variants{$variant_number}{alt_codon} = $alt_codon;
         $all_variants{$variant_number}{ref} = $ref;
         $all_variants{$variant_number}{alt} = $alt;
         $all_variants{$variant_number}{variant_fraction} = $variant_fraction;
	      $variant_number += 1;
      }

   }
   else
   {

      # print "$cds_hit\t$ref_aa\t$codon_number\t$alt_aa\t$N_or_S\t$position\t$position_in_cds\t$ref_codon\t$alt_codon\t$ref\t$alt\t$variant_fraction\n";
	   # $ref = "NA";
	   # $alt = "NA";
   }

}

sub reverse_complement
{
   my $seq = shift @_;
   $seq = reverse ($seq); 
   $seq =~ tr/ACGTacgt/TGCAtgca/;
   return $seq;
}

sub translate
{
   my $orf = uc $_[0];
   $orf =~ tr/T/U/;
   my $orf_length = length($orf);
   my $seq = "";
   if (($orf_length % 3) != 0)
   {
      die ("the length of orf $orf is not a multiple of 3 ($orf_length)\n");
   }
   for (my $i = 0; $i < $orf_length; $i+=3)
   {
      my $codon = uc(substr ($orf, $i, 3));
      my $residue = $genetic_code{"$codon"};
      if (!defined $residue)
      {
         # probably an ambiguous base
         $residue = "?";
      }
      elsif ($residue eq "*") 
      {
         if ($i != ($orf_length-3))
         {
            warn ("error: premature stop codon at position $i\norf: $orf\n")
         }
         # don't actually output an asterisk for stop codon
         $residue = "*";
      }
      $seq .= $residue;
   }
   return $seq;
}


sub initialize_genetic_code
{
   %genetic_code = (
   'UCA' => 'S', # Serine
   'UCC' => 'S', # Serine
   'UCG' => 'S', # Serine
   'UCU' => 'S', # Serine
   'UUC' => 'F', # Phenylalanine
   'UUU' => 'F', # Phenylalanine
   'UUA' => 'L', # Leucine
   'UUG' => 'L', # Leucine
   'UAC' => 'Y', # Tyrosine
   'UAU' => 'Y', # Tyrosine
   'UAA' => '*', # Stop
   'UAG' => '*', # Stop
   'UGC' => 'C', # Cysteine
   'UGU' => 'C', # Cysteine
   'UGA' => '*', # Stop
   'UGG' => 'W', # Tryptophan
   'CUA' => 'L', # Leucine
   'CUC' => 'L', # Leucine
   'CUG' => 'L', # Leucine
   'CUU' => 'L', # Leucine
   'CCA' => 'P', # Proline
   'CAU' => 'H', # Histidine
   'CAA' => 'Q', # Glutamine
   'CAG' => 'Q', # Glutamine
   'CGA' => 'R', # Arginine
   'CGC' => 'R', # Arginine
   'CGG' => 'R', # Arginine
   'CGU' => 'R', # Arginine
   'AUA' => 'I', # Isoleucine
   'AUC' => 'I', # Isoleucine
   'AUU' => 'I', # Isoleucine
   'AUG' => 'M', # Methionine
   'ACA' => 'T', # Threonine
   'ACC' => 'T', # Threonine
   'ACG' => 'T', # Threonine
   'ACU' => 'T', # Threonine
   'AAC' => 'N', # Asparagine
   'AAU' => 'N', # Asparagine
   'AAA' => 'K', # Lysine
   'AAG' => 'K', # Lysine
   'AGC' => 'S', # Serine
   'AGU' => 'S', # Serine
   'AGA' => 'R', # Arginine
   'AGG' => 'R', # Arginine
   'CCC' => 'P', # Proline
   'CCG' => 'P', # Proline
   'CCU' => 'P', # Proline
   'CAC' => 'H', # Histidine
   'GUA' => 'V', # Valine
   'GUC' => 'V', # Valine
   'GUG' => 'V', # Valine
   'GUU' => 'V', # Valine
   'GCA' => 'A', # Alanine
   'GCC' => 'A', # Alanine
   'GCG' => 'A', # Alanine
   'GCU' => 'A', # Alanine
   'GAC' => 'D', # Aspartic Acid
   'GAU' => 'D', # Aspartic Acid
   'GAA' => 'E', # Glutamic Acid
   'GAG' => 'E', # Glutamic Acid
   'GGA' => 'G', # Glycine
   'GGC' => 'G', # Glycine
   'GGG' => 'G', # Glycine
   'GGU' => 'G'  # Glycine
   );
}


sub parse_gff
{
   my $gff_fh = shift @_;
   my $fetching_seq = 0;
   my $seq = undef;
   my $seq_id = undef;
   
   while (<$gff_fh>)
   {
     chomp;
     if (/^#/ and not /##FASTA/)
     {
        next;
     }
     if (!$fetching_seq)
     {
         my @fields = split "\t";
         # Fields are: <seqname> <source> <feature> <start> <end> <score> <strand> <frame> [attributes] [comments]
         $seq_id = $fields[0];
         if ($fields[2] eq "CDS")
         {
            my $name = $fields[8];
            if ($name =~ /Name=(\w+)/)
            {
               $name = $1;
            }
            else
            {
               die "couldn't parse name for CDS: $_\n";
            }
            my $start = $fields[3];
            my $stop = $fields[4];

            # warn "$name $start->$stop\n";

            push @{$cds{$name}{start}}, $start;
            push @{$cds{$name}{stop}}, $stop;

            for (my $i = $start; $i <= $stop; $i++)
            {
               # warn "$i\t$name\n";
               push @{$basemap{$i}}, $name;
            }

            # todo: deal w/ - strand
            my $strand = $fields[6];
            if ($strand eq '-')
            {
               die "error: don't yet support neg strand CDS \n";
            }
         }
     }
     if (/##FASTA/)
     {
        $fetching_seq = 1;
        warn "found seq\n";
     }
     elsif($fetching_seq)
     {
        if (/>(\S+)/)
        {
           $seq_id = $1;
           # warn "seq_id: $seq_id\n";
        }
        else
        {
           $seq .= $_;
           # warn "seq: $seqs{$seq_id}\n";
        }
      }
   }

   my @cds_names = keys %cds;

   foreach my $cds_name (@cds_names)
   {
      # warn "CDS: $cds_name\n";
      # warn "seq: $seq\n";
      # get first start posn
      my @starts = sort @{$cds{$cds_name}{start}};   # TODO: co-sorting assumption OK?
      my @stops = sort @{$cds{$cds_name}{stop}};
      my @starts = @{$cds{$cds_name}{start}};  
      my @stops = @{$cds{$cds_name}{stop}};
      my $cds_position = 1;
      foreach my $start (@starts)
      {
         ## warn "** break **\n";
         my $stop = shift @stops;
         for (my $genome_position = $start; $genome_position <= $stop; $genome_position++)
         {
            # $cd_to_genome_map{$cds_name}{$cds_position} = $genome_position;
            $genome_to_cd_map{$cds_name}{$genome_position} = $cds_position;
            # warn "$genome_position\t$cds_name:$cds_position\n";

            # $cds_seqs{$cds_name}{$cds_position} = substr ($seq, ($cds_position-1), 1);
            # warn "cds_name $cds_name    genome_position:  $genome_position\n"; 
            $cds_seqs{$cds_name} .= substr ($seq, ($genome_position-1), 1);
            $cds_position += 1;
         }
      }
   }
}

